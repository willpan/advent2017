;; gorilla-repl.fileformat = 1

;; **
;;; # Gorilla REPL
;;; 
;;; Welcome to gorilla :-)
;;; 
;;; Shift + enter evaluates code. Hit ctrl+g twice in quick succession or click the menu icon (upper-right corner) for more commands ...
;;; 
;;; It's a good habit to run each worksheet in its own namespace: feel free to use the declaration we've provided below if you'd like.
;; **

;; @@
(ns advent
  (:require [clojure.string :as str]))

(defn input [n]
  (str/trim (slurp (str "inputs/day" n ".txt"))))

(defn parse-int [s]
  (Integer/parseInt (str s)))

(defn assert-eq [a b]
  (assert (= a b)))
;; @@
;; =>
;;; {"type":"html","content":"<span class='clj-var'>#&#x27;advent/assert-eq</span>","value":"#'advent/assert-eq"}
;; <=

;; **
;;; # Day 1
;;; 
;; **

;; @@
(defn captcha-1
  "Calculate the captcha of a text string by summing up
  chars that are followed by the same character, wrapping
  around the first character."
  [text]
  (->> (str text (first text))
       (map parse-int)
       (partition 2 1)
       (filter #(apply = %))
       (map first)
       (reduce +)))


(captcha-1 (input 1))
;; @@
;; =>
;;; {"type":"html","content":"<span class='clj-long'>1175</span>","value":"1175"}
;; <=

;; @@
(defn rotate-str [n s]
  (->> (cycle s) (drop n) (take (count s)) (apply str)))

(defn captcha-2
  "Calculate the captcha of a text string by summing up
  chars where the char halfway around the string is the same."
  [text]
  (let [half-length (/ (count text) 2)
        text' (rotate-str half-length text)]
    (reduce + (map #(if (= %1 %2) (parse-int %1) 0) text text'))))


(captcha-2 (input 1))

;; @@
;; =>
;;; {"type":"html","content":"<span class='clj-long'>1166</span>","value":"1166"}
;; <=
